plugins {
    id 'java-library'
    id 'org.springframework.boot' version '3.3.3' apply false
    id 'io.spring.dependency-management' version '1.1.6'
    id 'maven-publish'
}

group = 'com.bytmasoft'
version = '1.0.1'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'jwt-security-lib'
            version = '1.0.1'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'jwt security lib'
                description = 'Jwt Security lib use for all services to authentication'
                url = 'http://localhost/library'
                developers {
                    developer {
                        id = 'Abakar'
                        name = 'Abakar Mahamat'
                        email = 'abakar61@web.de'
                    }
                }
                scm {

                    connection = 'scm:git@github.com:Abakar14/dss-common.git'
                    developerConnection = 'scm:git@github.com:Abakar14/jwt-security-lib.git'
                    url = 'https://github.com/Abakar14/jwt-security-lib/'
                }
            }
        }
    }
    repositories {
        maven {
            // change URLs to point to your repos, e.g. http://my.org/repo
            def releasesRepoUrl = layout.buildDirectory.dir('/home/abakar/.m2/repository')
            url = releasesRepoUrl
        }
    }
}
repositories {
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation group: 'io.jsonwebtoken', name: 'jjwt-api', version: "${jjwtVersion}"
    runtimeOnly group: 'io.jsonwebtoken', name: 'jjwt-impl', version:  "${jjwtVersion}"
    runtimeOnly group: 'io.jsonwebtoken', name: 'jjwt-jackson', version: "${jjwtVersion}"
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()
}
